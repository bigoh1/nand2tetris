// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Or (a=load, b=inc, out=loadORinc);
    Or (a=reset, b=loadORinc, out=wOr);

    Register (in=wReset, load=wOr, out=out, out=wReg);

    Inc16 (in=wReg, out=wInc);
    Mux16 (a=wReg, b=wInc, sel=inc, out=wAfterInc);

    Mux16 (a=wAfterInc, b=in, sel=load, out=wLoad);

    Mux16 (a=wLoad, b=false, sel=reset, out=wReset);
}
